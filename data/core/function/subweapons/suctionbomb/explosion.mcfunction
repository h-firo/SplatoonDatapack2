#演出
playsound item.bucket.empty_axolotl master @a ~ ~ ~ 1
playsound minecraft:entity.generic.explode master @a ~ ~ ~ 0.2 1.5
particle explosion ~ ~ ~ 0 0 0 0 1
function core:subweapons/suctionbomb/particle with entity @s data
execute positioned ~ ~0.5 ~ run function core:subweapons/suctionbomb/particle with entity @s data
execute positioned ~ ~1 ~ run function core:subweapons/suctionbomb/particle with entity @s data
execute positioned ~ ~1.5 ~ run function core:subweapons/suctionbomb/particle with entity @s data
execute positioned ~ ~2 ~ run function core:subweapons/suctionbomb/particle with entity @s data

#塗り
$fill ~-3 ~-1 ~-3 ~3 ~2 ~3 $(color)_concrete replace #core:can_inking
$scoreboard players add @a[level=$(owner)] specialPoint 30

#攻撃
$execute as @e[distance=..3,tag=player,tag=!bomb$(bNum)] unless entity @s[level=$(owner)] unless score @s team matches $(team) run function core:damages/remove_health {value:180,type:"suctionbomb",killer:$(owner)}
$execute as @e[distance=..3.8,tag=player,tag=!bomb$(bNum)] unless entity @s[level=$(owner)] unless score @s team matches $(team) run function core:damages/remove_health {value:30,type:"suctionbomb",killer:$(owner)}
$execute as @e[distance=..3.8,tag=player,tag=!bomb$(bNum)] at @s unless entity @s[level=$(owner)] unless score @s team matches $(team) run playsound entity.player.hurt master @a ~ ~ ~ 1
#$execute if entity @e[distance=..1.7,tag=player,tag=!attack$(shotNum)] unless entity @e[distance=..1.7,tag=player,tag=!attack$(shotNum),level=$(owner)] kill @s
$execute as @e[distance=..3.8,tag=player] unless entity @s[level=$(owner)] unless score @s team matches 0 run tag @s add bomb$(bNum)